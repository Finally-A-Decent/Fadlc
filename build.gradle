plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'info.preva1l'
version = "1.0.0"

ext {
    set 'version', version.toString()
    set 'configlib_version', configlib_version.toString()
    set 'acp2_version', acp2_version.toString()
    set 'jedis_version', jedis_version.toString()
    set 'hikari_version', hikari_version.toString()
    set 'sqlite_version', sqlite_version.toString()
    set 'mysql_version', mysql_version.toString()
    set 'mariadb_version', mariadb_version.toString()
    set 'mongo_version', mongo_version.toString()
    set 'influxdb_version', influxdb_version.toString()
    set 'minimessage_version', minimessage_version.toString()
    set 'mm_platform_version', mm_platform_version.toString()
}

allprojects {
    apply plugin: 'com.gradleup.shadow'
    apply plugin: 'maven-publish'
    apply plugin: 'java'

    sourceCompatibility = 21
    targetCompatibility = 21

    compileJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
        maven { url "https://repo.papermc.io/repository/maven-public/" }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.0.0'
        compileOnly 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.jetbrains:annotations:24.0.0'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

        testCompileOnly 'org.jetbrains:annotations:24.1.0'
        testCompileOnly 'org.projectlombok:lombok:1.18.32'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
    }

    test {
        useJUnitPlatform()
    }

    processResources {
        def tokenMap = rootProject.ext.properties
        filesMatching(['**/*.json', '**/*.yml']) {
            expand tokenMap
        }
    }
}

subprojects {
    version rootProject.version
    archivesBaseName = "${rootProject.name}-${project.name}"

    shadowJar {
        destinationDirectory.set(file("$rootDir/target"))
        archiveClassifier.set('')
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier.set('javadoc')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact shadowJar
                artifact(tasks.named('sourceJar'))
                artifact(tasks.named('javadocJar'))
            }
        }
    }

    jar.dependsOn shadowJar
    clean.delete "$rootDir/target"
}

logger.lifecycle("Building Fadlc ${version}")